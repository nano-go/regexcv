plugins {
    id 'java'
    id 'application'
    id "com.diffplug.spotless" version "6.12.0"
}

version = 'v1.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs/', include: ['*.jar'])
    implementation 'commons-cli:commons-cli:1.4'
    testImplementation 'junit:junit:4.13'
}

application {
    mainClass = 'com.nano.regexcv.Main'
}

compileJava {
    options.compilerArgs << "-Xlint:deprecation"
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass,
                'Implementation-Version': version,
                'Implementation-Title': 'regexcv'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }

    java {
        licenseHeaderFile rootProject.file('license-header.java')
        importOrder('java', 'javax', 'com', 'org')
        googleJavaFormat()
        formatAnnotations()
    }
}
