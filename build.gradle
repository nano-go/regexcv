plugins {
    id 'java'
    id 'application'
    id 'jacoco-report-aggregation'
    id "com.diffplug.spotless" version "6.12.0"
}

version = 'v1.0.1'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs/', include: ['*.jar'])
    implementation 'commons-cli:commons-cli:1.4'
    testImplementation 'junit:junit:4.13'
}

application {
    mainClassName = 'com.nano.regexcv.Main'
}

compileJava {
    options.compilerArgs << "-Xlint:deprecation"
}


jar {
    manifest {
        attributes 'Main-Class': application.mainClassName,
                'Implementation-Version': version,
                'Implementation-Title': 'regexcv'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled false
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
                indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }

    java {
        licenseHeaderFile rootProject.file('license-header.java')
        importOrder('java', 'javax', 'com', 'org')
        googleJavaFormat()
        formatAnnotations()
    }
}
